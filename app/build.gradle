apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.ajoberstar.grgit'
apply from: "https://gist.githubusercontent.com/danielsanfr/6bd03fd981b3940863a843fe748794a9/raw/apk-better-name.gradle"

Properties signatureProperties = new Properties()
file("../signature.properties").withInputStream { signatureProperties.load(it) }

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "br.com.danielsan.notafiscalcidada"
        minSdkVersion 16
        targetSdkVersion 26
        multiDexEnabled true
        versionCode grgit.tag.list().size() ?: 0
        versionName grgit.describe() ?: "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dataBinding {
        enabled true
    }
    signingConfigs {
        debug { }
        release {
            keyAlias signatureProperties.getProperty("keyAlias")
            keyPassword signatureProperties.getProperty("keyPassword")
            storeFile file(signatureProperties.getProperty("storeFile"))
            storePassword signatureProperties.getProperty("storePassword")
        }
    }
    buildTypes {
        fake {
            debuggable true
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
        debugWithProGuard {
            debuggable true
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        releaseDebuggable {
            debuggable true
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
        fake {
            manifest.srcFile '/src/debug/AndroidManifest.xml'
            java.srcDirs = ['src/fake/java', 'src/fake/kotlin']
            resources.srcDirs = ['src/fake/java', 'src/fake/kotlin']
            aidl.srcDirs = ['src/fake/java', 'src/fake/kotlin']
            renderscript.srcDirs = ['src/fake/java', 'src/fake/kotlin']
            res.srcDirs = ['src/debug/res']
            assets.srcDirs = ['src/debug/assets']
        }
        debugWithProGuard {
            manifest.srcFile '/src/debug/AndroidManifest.xml'
            java.srcDirs = ['src/debug/java', 'src/debug/kotlin']
            resources.srcDirs = ['src/debug/java', 'src/debug/kotlin']
            aidl.srcDirs = ['src/debug/java', 'src/debug/kotlin']
            renderscript.srcDirs = ['src/debug/java', 'src/debug/kotlin']
            res.srcDirs = ['src/debug/res']
            assets.srcDirs = ['src/debug/assets']
        }
        releaseDebuggable {
            manifest.srcFile '/src/release/AndroidManifest.xml'
            java.srcDirs = ['src/release/java', 'src/release/kotlin']
            resources.srcDirs = ['src/release/java', 'src/release/kotlin']
            aidl.srcDirs = ['src/release/java', 'src/release/kotlin']
            renderscript.srcDirs = ['src/release/java', 'src/release/kotlin']
            res.srcDirs = ['src/release/res']
            assets.srcDirs = ['src/release/assets']
        }
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation "com.android.support:multidex:1.0.2"
    implementation "com.android.support:design:$appcompat_version"
    implementation "com.android.support:cardview-v7:$appcompat_version"
    implementation "com.android.support:appcompat-v7:$appcompat_version"
    implementation "com.android.support:recyclerview-v7:$appcompat_version"
    implementation "com.android.support.constraint:constraint-layout:1.0.2"

    implementation "com.google.firebase:firebase-core:$play_services_version"
    implementation "com.google.android.gms:play-services-oss-licenses:$play_services_version"

    implementation "io.reactivex.rxjava2:rxjava:2.1.7"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"

    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:3.9.1"

    implementation "com.jakewharton.timber:timber:4.6.0"

    implementation "com.google.code.gson:gson:2.8.2"

    implementation "com.dlazaro66.qrcodereaderview:qrcodereaderview:2.0.3"

    implementation "com.github.Ilhasoft:data-binding-validator:1.0.0"

    implementation "com.github.ilhasoft.rapidpro-android-sdk:fcm-channel:1.0.4"

    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"

    implementation("com.crashlytics.sdk.android:crashlytics:2.7.1@aar") {
        transitive = true
    }

    implementation("br.com.jansenfelipe:androidmask:1.0.1") {
        exclude module: "support-v4"
    }

    implementation("com.github.hotchemi:permissionsdispatcher:$permission_dispatcher_version") {
        exclude module: "support-v13"
    }

    implementation("com.github.bumptech.glide:glide:$glide_version") {
        exclude group: "com.android.support", module: "support-compat"
        exclude group: "com.android.support", module: "support-v4"
    }

    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"

    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    kapt "com.github.hotchemi:permissionsdispatcher-processor:$permission_dispatcher_version"

    kapt "com.android.databinding:compiler:$android_plugin_version"

    kapt "com.github.bumptech.glide:compiler:$glide_version"

    testImplementation "junit:junit:4.12"

    androidTestImplementation "com.android.support.test:runner:1.0.1"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.1"
}

apply plugin: 'com.google.gms.google-services'
